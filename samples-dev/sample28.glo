! KEYBOARD_INPUT: 2,2
! KEYBOARD_INPUT: 1,1
! KEYBOARD_INPUT: 1,3
! KEYBOARD_INPUT: 2,1
! KEYBOARD_INPUT: 2,3
! KEYBOARD_INPUT: 3,2
! KEYBOARD_INPUT: 3,1
! KEYBOARD_INPUT: 3,3
! KEYBOARD_INPUT: 1,2

ΠΡΟΓΡΑΜΜΑ ασκ48_54
ΜΕΤΑΒΛΗΤΕΣ
  ΑΚΕΡΑΙΕΣ: i, j, πλ, παικτηςσειρά
  ΧΑΡΑΚΤΗΡΕΣ: Τ[3, 3] 
  ΛΟΓΙΚΕΣ: νικ1παίκτης, νικ2παίκτης, αποτ

ΑΡΧΗ
  ΓΙΑ i ΑΠΟ 1 ΜΕΧΡΙ 3
    ΓΙΑ j ΑΠΟ 1 ΜΕΧΡΙ 3
      Τ[i, j] <- "-"
    ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ

  νικ1παίκτης <- ΨΕΥΔΗΣ
  νικ2παίκτης <- ΨΕΥΔΗΣ

  πλ <- 0

  ΑΡΧΗ_ΕΠΑΝΑΛΗΨΗΣ
    πλ <- πλ + 1

    ΑΝ πλ mod 2 <> 0 ΤΟΤΕ
      παικτηςσειρά <- 1
    ΑΛΛΙΩΣ
      παικτηςσειρά <- 2
    ΤΕΛΟΣ_ΑΝ

    ΑΡΧΗ_ΕΠΑΝΑΛΗΨΗΣ
      ΓΡΑΨΕ 'Παίκτη: ', παικτηςσειρά, ' δώσε συντενταγμένες:'
      ΔΙΑΒΑΣΕ i, j
    ΜΕΧΡΙΣ_ΟΤΟΥ Τ[i, j] = "-"

    ΑΝ πλ mod 2 <> 0 ΤΟΤΕ
      Τ[i, j] <- "Χ" ! Παίκτης 1
      νικ1παίκτης <- ΝΙΚΗΣΕ(Τ, i, j) 
    ΑΛΛΙΩΣ
      Τ[i, j] <- "Ο" ! Παίκτης 2
      νικ2παίκτης <- ΝΙΚΗΣΕ(Τ, i, j) 
    ΤΕΛΟΣ_ΑΝ

  ΜΕΧΡΙΣ_ΟΤΟΥ πλ = 9 Η νικ1παίκτης = ΑΛΗΘΗΣ Η νικ2παίκτης = ΑΛΗΘΗΣ

  ΑΝ νικ1παίκτης = ΑΛΗΘΗΣ ΤΟΤΕ
    ΓΡΑΨΕ 'Νίκησε ο 1ος παίκτης'
  ΑΛΛΙΩΣ_ΑΝ νικ2παίκτης = ΑΛΗΘΗΣ ΤΟΤΕ
    ΓΡΑΨΕ 'Νίκησε ο 2ος παίκτης'
  ΑΛΛΙΩΣ
    ΓΡΑΨΕ 'Ισοπαλία'
  ΤΕΛΟΣ_ΑΝ

ΤΕΛΟΣ_ΠΡΟΓΡΑΜΜΑΤΟΣ

ΣΥΝΑΡΤΗΣΗ ΝΙΚΗΣΕ(ΠΙΝ, Γ, Σ): ΛΟΓΙΚΗ
ΜΕΤΑΒΛΗΤΕΣ
  ΧΑΡΑΚΤΗΡΕΣ: ΠΙΝ[3, 3] 
  ΑΚΕΡΑΙΕΣ: Γ, Σ
  ΛΟΓΙΚΕΣ: αποτ
ΑΡΧΗ
  αποτ <- ΨΕΥΔΗΣ

  ΑΝ ΠΙΝ[Γ, 1] = ΠΙΝ[Γ, 2] ΚΑΙ ΠΙΝ[Γ, 2] = ΠΙΝ[Γ, 3] ΤΟΤΕ
    αποτ <- ΑΛΗΘΗΣ
  ΤΕΛΟΣ_ΑΝ

  ΑΝ ΠΙΝ[1, Σ] = ΠΙΝ[2, Σ] ΚΑΙ ΠΙΝ[2, Σ] = ΠΙΝ[3, Σ] ΤΟΤΕ
    αποτ <- ΑΛΗΘΗΣ
  ΤΕΛΟΣ_ΑΝ

  ΑΝ Γ = Σ ΤΟΤΕ
    ΑΝ ΠΙΝ[1, 1] = ΠΙΝ[2, 2] ΚΑΙ ΠΙΝ[2, 2] = ΠΙΝ[3, 3] ΤΟΤΕ
      αποτ <- ΑΛΗΘΗΣ
    ΤΕΛΟΣ_ΑΝ
  ΤΕΛΟΣ_ΑΝ

  ΑΝ Γ + Σ = 4 ΤΟΤΕ
    ΑΝ ΠΙΝ[1, 3] = ΠΙΝ[2, 2] ΚΑΙ ΠΙΝ[2, 2] = ΠΙΝ[3, 1] ΤΟΤΕ
      αποτ <- ΑΛΗΘΗΣ
    ΤΕΛΟΣ_ΑΝ
  ΤΕΛΟΣ_ΑΝ

  ΑΝ αποτ = ΑΛΗΘΗΣ ΤΟΤΕ
    ΝΙΚΗΣΕ <- ΑΛΗΘΗΣ
  ΑΛΛΙΩΣ
    ΝΙΚΗΣΕ <- ΨΕΥΔΗΣ
  ΤΕΛΟΣ_ΑΝ
ΤΕΛΟΣ_ΣΥΝΑΡΤΗΣΗΣ



