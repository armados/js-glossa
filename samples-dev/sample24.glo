! KEYBOARD_INPUT: 500
! KEYBOARD_INPUT: 'Αρμαντος','Νίκος'
! KEYBOARD_INPUT: 22
! KEYBOARD_INPUT: 0
ΠΡΟΓΡΑΜΜΑ ΛΟΓΑΡΙΑΣΜΟΣ_ΝΕΡΟΥ
ΣΤΑΘΕΡΕΣ
  ΦΠΑ = 0.18
  ΤΙΜΗ1 = 0.34
  ΤΙΜΗ2 = 0.52
  ΤΙΜΗ3 = 1.51
  ΤΙΜΗ4 = 2.11
  ΤΙΜΗ5 = 2.64
  ΠΑΓΙΟ = 1.47
ΜΕΤΑΒΛΗΤΕΣ
  ΑΚΕΡΑΙΕΣ: Μετρητής
  ΠΡΑΓΜΑΤΙΚΕΣ: Κατανάλωση, Τιμή, Αποχέτευση, Άλλα, Αξία_ΦΠΑ, Αξία,
  & Τελική_Τιμή
  ΧΑΡΑΚΤΗΡΕΣ: Όνομα, Επώνυμο
ΑΡΧΗ
  ΓΡΑΨΕ 'Δώσε Αριθμό του μετρητή (0 για τέλος)'
  ΔΙΑΒΑΣΕ Μετρητής
  ΟΣΟ Μετρητής <> 0 ΕΠΑΝΑΛΑΒΕ
    ΓΡΑΨΕ 'Δώσε το Ονοματεπώνυμο'
    ΔΙΑΒΑΣΕ Επώνυμο, Όνομα
    ΓΡΑΨΕ 'Δώσε την Κατανάλωση'
    ΔΙΑΒΑΣΕ Κατανάλωση
    ΕΠΙΛΕΞΕ Κατανάλωση
      ΠΕΡΙΠΤΩΣΗ 0..15
        Αξία <- Κατανάλωση*ΤΙΜΗ1
      ΠΕΡΙΠΤΩΣΗ 15..60
        Αξία <- 15*ΤΙΜΗ1 + (Κατανάλωση - 15)*ΤΙΜΗ2
      ΠΕΡΙΠΤΩΣΗ 60..81
        Αξία <- 15*ΤΙΜΗ1 + 45*ΤΙΜΗ2 + (Κατανάλωση - 60)*ΤΙΜΗ3
      ΠΕΡΙΠΤΩΣΗ 81..105
        Αξία <- 15*ΤΙΜΗ1 + 45*ΤΙΜΗ2 + 21*ΤΙΜΗ3 + (Κατανάλωση - 81)*ΤΙΜΗ4
      ΠΕΡΙΠΤΩΣΗ ΑΛΛΙΩΣ
        Αξία <- 15*ΤΙΜΗ1 + 45*ΤΙΜΗ2 + 21*ΤΙΜΗ3 + 24*ΤΙΜΗ4 + (Κατανάλωση - 105)*ΤΙΜΗ5
    ΤΕΛΟΣ_ΕΠΙΛΟΓΩΝ
    Αποχέτευση <- Αξία*0.4
    Άλλα <- Αξία*0.01
    Τιμή <- Αξία + Αποχέτευση + Άλλα + ΠΑΓΙΟ
    Αξία_ΦΠΑ <- Τιμή* ΦΠΑ
    Τελική_Τιμή <- Τιμή + Αξία_ΦΠΑ
    ΓΡΑΨΕ 'Ο λογαριασμός του ', Επώνυμο, ' είναι ', Τελική_Τιμή
    ΓΡΑΨΕ 'Αξία νερού:', Αξία
    ΓΡΑΨΕ 'άλλα:', Αποχέτευση + Άλλα, 'ΦΠΑ:', Αξία_ΦΠΑ
    ΓΡΑΨΕ 'Δώσε Αριθμό του επόμενου μετρητή (0 για τέλος)'
    ΔΙΑΒΑΣΕ Μετρητής
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
ΤΕΛΟΣ_ΠΡΟΓΡΑΜΜΑΤΟΣ