! KEYBOARD_INPUT: 20
! KEYBOARD_INPUT: 1050,4598,1992,3517,4739,4399,3117,3492,1201,2897,2571,2714,3397,2125,1444,2097,1989,3979,2678,1622
! KEYBOARD_INPUT: 10
! KEYBOARD_INPUT: 9368,9258,9287,9320,9130,9469,9358,9005,9500,9093

ΠΡΟΓΡΑΜΜΑ Συγχώνευση 
ΜΕΤΑΒΛΗΤΕΣ 
ΑΚΕΡΑΙΕΣ: Α[100], B[100], Γ[200], I, J, Κ, Ν, Μ, Λ 
! Α και Β αρχικοί πίνακες 
! Γ τελικός πίνακας 
ΑΡΧΗ 

! Διάβασε τα δεδομένα 
ΓΡΑΨΕ 'Δώσε το πλήθος των στοιχείων του πίνακα Α (<100)' 
ΔΙΑΒΑΣΕ Ν 
ΓΙΑ I ΑΠΟ 1 ΜΕΧΡΙ Ν 
    ΔΙΑΒΑΣΕ Α[I] 
ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
ΓΡΑΨΕ 'Δώσε το πλήθος των στοιχείων του πίνακα Β(<100)'
ΔΙΑΒΑΣΕ Μ 
ΓΙΑ I ΑΠΟ 1 ΜΕΧΡΙ Μ 
    ΔΙΑΒΑΣΕ B[I] 
ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ 

! Συγχώνευση πινάκων
! I είναι ο δείκτης για τον πίνακα Α 
! J είναι ο δείκτης για τον πίνακα Β 
! Κ είναι ο δείκτης για τον πίνακα Γ 
I <- 1 
J <- 1 
Κ <- 1 
ΟΣΟ I <= Ν ΚΑΙ J <= Μ ΕΠΑΝΑΛΑΒΕ 
! Όσο και τα δύο έχουν στοιχεία 
    ΑΝ Α[I] < B[J] ΤΟΤΕ 
       Γ[Κ] <- Α[I] 
       Κ <- Κ+1 
       I <- I+1 
    ΑΛΛΙΩΣ 
       Γ[Κ] <- B[J] 
       Κ <- Κ+1 
       J <- J +1 
    ΤΕΛΟΣ_ΑΝ 
ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ 

! Μεταφορά των υπολοίπων στοιχείων του Α ή του Β 
ΑΝ I > Ν ΤΟΤΕ 
   ΓΙΑ Λ ΑΠΟ Κ ΜΕΧΡΙ Ν+Μ 
       Γ[Λ] <- B[J] 
       J <- J +1 
   ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ 
ΑΛΛΙΩΣ 
   ΓΙΑ Λ ΑΠΟ Κ ΜΕΧΡΙ Ν+Μ 
       Γ[Λ] <- Α[I] 
       I <- I+1 
   ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ 
ΤΕΛΟΣ_ΑΝ

! Εκτύπωση τελικού πίνακα 
ΓΙΑ Λ ΑΠΟ 1 ΜΕΧΡΙ Ν+Μ 
    ΓΡΑΨΕ Γ[Λ] 
ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ 
ΤΕΛΟΣ_ΠΡΟΓΡΑΜΜΑΤΟΣ